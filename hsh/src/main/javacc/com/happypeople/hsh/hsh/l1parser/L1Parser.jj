options
{
	STATIC=false;
	USER_TOKEN_MANAGER=false;
	FORCE_LA_CHECK=true;
	TOKEN_MANAGER_USES_PARSER=true;
	DEBUG_PARSER=false;
	DEBUG_TOKEN_MANAGER=false;
}
PARSER_BEGIN(L1Parser)
/** http://pubs.opengroup.org/onlinepubs/009695399/utilities/xcu_chap02.html#tag_02_02
 ** 2.3 Token Recognition
* The shell shall read its input in terms of lines from a file, from a terminal in the case of an interactive shell,
* or from a string in the case of sh -c or system(). The input lines can be of unlimited length.
* These lines shall be parsed using two major modes: ordinary token recognition and processing of here-documents.

* When an io_here token has been recognized by the grammar (see Shell Grammar), one or more of the subsequent lines
* immediately following the next NEWLINE token form the body of one or more here-documents and shall be parsed according
* to the rules of Here-Document.

* When it is not processing an io_here, the shell shall break its input into tokens by applying the first applicable
* rule below to the next character in its input. The token shall be from the current position in the input until a
* token is delimited according to one of the rules below; the characters forming the token are exactly those in the
* input, including any quoting characters. If it is indicated that a token is delimited, and no characters have been
* included in a token, processing shall continue until an actual token is delimited.

* 1. If the end of input is recognized, the current token shall be delimited. If there is no current token,
* 	the end-of-input indicator shall be returned as the token.

* 2. If the previous character was used as part of an operator and the current character is not quoted and can be used
* 	with the current characters to form an operator, it shall be used as part of that (operator) token.

* 3. If the previous character was used as part of an operator and the current character cannot be used with the current
* 	characters to form an operator, the operator containing the previous character shall be delimited.

* 4. If the current character is backslash, single-quote, or double-quote ( '\', '", or ' )' and it is not quoted,
* 	it shall affect quoting for subsequent characters up to the end of the quoted text. The rules for quoting are as
* 	described in Quoting. During token recognition no substitutions shall be actually performed, and the result token
* 	shall contain exactly the characters that appear in the input (except for <newline> joining), unmodified, including
* 	any embedded or enclosing quotes or substitution operators, between the quote mark and the end of the quoted text.
* 	The token shall not be delimited by the end of the quoted field.

* 5. If the current character is an unquoted '$' or '`', the shell shall identify the start of any candidates for
* 	parameter expansion ( Parameter Expansion), command substitution ( Command Substitution), or arithmetic expansion
* 	( Arithmetic Expansion) from their introductory unquoted character sequences: '$' or "${", "$(" or '`', and
*	 "$((", respectively. The shell shall read sufficient input to determine the end of the unit to be expanded
*	(as explained in the cited sections). While processing the characters, if instances of expansions or quoting are
*	found nested within the substitution, the shell shall recursively process them in the manner specified for the
*	construct that is found. The characters found from the beginning of the substitution to its end, allowing for any
*	recursion necessary to recognize embedded constructs, shall be included unmodified in the result token, including any
*	embedded or enclosing substitution operators or quotes. The token shall not be delimited by the end of the substitution.

* 6. If the current character is not quoted and can be used as the first character of a new operator, the current token
* 	(if any) shall be delimited. The current character shall be used as the beginning of the next (operator) token.

* 7. If the current character is an unquoted <newline>, the current token shall be delimited.

* 8. If the current character is an unquoted <blank>, any token containing the previous character is delimited and the
* 	current character shall be discarded.

* 9. If the previous character was part of a word, the current character shall be appended to that word.

* 10. If the current character is a '#', it and all subsequent characters up to, but excluding, the next <newline>
* shall be discarded as a comment. The <newline> that ends the line is not considered part of the comment.

* 11. The current character is used as the start of a new word.

* Once a token is delimited, it is categorized as required by the grammar in Shell Grammar.

********************************************************************************************
2.2.3 Double-Quotes

Enclosing characters in double-quotes ( "" ) shall preserve the literal value of all characters within the double-quotes,
with the exception of the characters dollar sign, backquote, and backslash, as follows:

$
The dollar sign shall retain its special meaning introducing parameter expansion (see Parameter Expansion), a form of
command substitution (see Command Substitution), and arithmetic expansion (see Arithmetic Expansion).
The input characters within the quoted string that are also enclosed between "$(" and the matching ')' shall not be
affected by the double-quotes, but rather shall define that command whose output replaces the "$(...)" when the word is
expanded. The tokenizing rules in Token Recognition , not including the alias substitutions in Alias Substitution , shall
be applied recursively to find the matching ')'.

Within the string of characters from an enclosed "${" to the matching '}', an even number of unescaped double-quotes or
single-quotes, if any, shall occur. A preceding backslash character shall be used to escape a literal '{' or '}'.
The rule in Parameter Expansion shall be used to determine the matching '}' .

`
The backquote shall retain its special meaning introducing the other form of command substitution
(see Command Substitution). The portion of the quoted string from the initial backquote and the characters up to the
next backquote that is not preceded by a backslash, having escape characters removed, defines that command whose output
replaces "`...`" when the word is expanded.
Either of the following cases produces undefined results:
A single-quoted or double-quoted string that begins, but does not end, within the "`...`" sequence
A "`...`" sequence that begins, but does not end, within the same double-quoted string

\
The backslash shall retain its special meaning as an escape character (see Escape Character (Backslash)) only when
followed by one of the following characters when considered special:
$   `   "   \   <newline>

The application shall ensure that a double-quote is preceded by a backslash to be included within double-quotes.
The parameter '@' has special meaning inside double-quotes and is described in Special Parameters.
********************************************************************************************
http://pubs.opengroup.org/onlinepubs/009695399/utilities/xcu_chap02.html#tag_02_09_01
The following are the operators mentioned above.


AND_IF    OR_IF    DSEMI
 '&&'      '||'     ';;'    


DLESS  DGREAT  LESSAHND  GREATAND  LESSGREAT  DLESSDAS
 '<<'   '>>'    '<&'     '>&'      '<>'       '<<-'   


 CLOBBER
   '>|'

* ******************************************************************************************
* Tokenizer States tree:
* -DEFAULT
*	-BACKSLASH escapes any following char: BQUOTED
*		-following char : DEFAULT
*	-SINGLEQUOTE escapes all chars: SQUOTED
*		-SINGLEQUOTE : DEFAULT
*	-DOUBLEQOUTE : DQUOTED
*		-BACKSLASH escapes: $, BACKTIC, BACKSLASH, DOUBLEQUOTE
*		-$ starts substitution : DOUBLEQUOTE_DOLLAR
*		-BACKTIC start substitution : DOUBLEQUOTE_BACKTICKED
*		-DOUBLEQUOTE : DEFAULT
*	-BACKTIC : DEFAULT_BACKTICKED
*		--TODO
*/

package com.happypeople.hsh.hsh.l1parser;
import java.util.*;
import com.happypeople.hsh.hsh.L2Token;
import com.happypeople.hsh.hsh.HshParserConstants;

public class L1Parser // implements TokenManager
{
	final boolean DEBUG=false;
	
	/** Stack of states. Usefull to switch to "previous" state. Initialized to DEFAULT. */
	private LinkedList<Integer> stateStack=new LinkedList<Integer>(Arrays.asList(DEFAULT));

	/** Called by TokenManager */
	void pushState(int state) {
		if(DEBUG)
			System.out.println("pushState: "+state);
		stateStack.push(state);
		token_source.SwitchTo(state);
	}

	/** Called by TokenManager */
	void popState() {
		final int oldstate=stateStack.pop();
		final int state=stateStack.peek();
		if(DEBUG)
			System.out.println("popState, from:"+oldstate+" to:"+state);
		token_source.SwitchTo(state);
	}

	/** Interface for token-translation between L1Parser and L2Parser
	*/
	public L2Token nextL1Node() throws ParseException {
		L2Token t;
		do {			t=word();
			if(DEBUG)
				System.out.println("nextL1Node: "+t.kind+" "+t.image);
		} while(t.kind==WS_KIND); // skip WS
		return t;
	}

	/** This needs to be defined, because in L2Parser there is no WS.
	*	Note that WS-Token are not sent to L2Parser.
	* 	It is defined as -42, because this is an unused number for Token-constants (they are all >=0)
	*/
	private final static int WS_KIND=-42;
	private final static int UNDEFINED_KIND=-1;

	private final static int[] kindMap=new int[Math.max(
		L1ParserConstants.tokenImage.length,
		HshParserConstants.tokenImage.length)];
	static {
		for(int i=0; i<kindMap.length; i++)
			kindMap[i]=UNDEFINED_KIND;

		kindMap[L1ParserConstants.EOF]=HshParserConstants.EOF;
		kindMap[L1ParserConstants.AND_IF]=HshParserConstants.AND_IF;
		kindMap[L1ParserConstants.OR_IF]=HshParserConstants.OR_IF;
		kindMap[L1ParserConstants.DSEMI]=HshParserConstants.DSEMI;
		kindMap[L1ParserConstants.DLESS]=HshParserConstants.DLESS;
		kindMap[L1ParserConstants.DGREAT]=HshParserConstants.DGREAT;
		kindMap[L1ParserConstants.GREAT]=HshParserConstants.GREAT;
		kindMap[L1ParserConstants.GREATAND]=HshParserConstants.GREATAND;
		kindMap[L1ParserConstants.LESSAND]=HshParserConstants.LESSAND;
		kindMap[L1ParserConstants.LESS]=HshParserConstants.LESS;
		kindMap[L1ParserConstants.LESSGREAT]=HshParserConstants.LESSGREAT;
		kindMap[L1ParserConstants.DLESSDASH]=HshParserConstants.DLESSDASH;
		kindMap[L1ParserConstants.CLOBBER]=HshParserConstants.CLOBBER;
		kindMap[L1ParserConstants.NEWLINE]=HshParserConstants.NEWLINE;
		kindMap[L1ParserConstants.SEMICOLON]=HshParserConstants.SEMICOLON;
		kindMap[L1ParserConstants.UPPERSANT]=HshParserConstants.UPPERSANT;
		kindMap[L1ParserConstants.WS]=WS_KIND;
		// TODO others ???
	}

	/** Returns the L2-kind according to l1kind
	 * @param kind L1 kind
	 * @return L2 kind
	 */
	private int translateL1KindToL2Kind(final int l1kind) {
		final int l2kind=kindMap[l1kind];
		if(l2kind==UNDEFINED_KIND)
			throw new RuntimeException("internal error: cannot map L1-Kind: "+l1kind);
		return l2kind;
	}

	/** Interface for dynamic extra actions. */
	interface Action {
		public void after();
	}
}

PARSER_END(L1Parser)

TOKEN:
{	< #SQUOTE: "'" >
|	< #DQUOTE: "\"" > 
|	< #BQUOTE: "\\" >
|	< #BACKTIC:	"`" >
//|	< #def_DOLLAR:	"$" >
|	< #BANYTHING: ~[] >
|	< #DEF_ESC_BACKSLASH	: "\\\\" >
|	< #def_DEFAULT_ANYTHING	: (~["`", "'", "\"", "\\", 
								" ", "\t", "\r", "\n", 
								"&", "|", ";", "<", ">", "$", "{", 
								"}", "(", ")" ])+ >
	// chars of DEFAULT_ANYTHING and DO_SPECIAL
|	< #def_DA_AND_DO_SPECIAL	:  ~["`", "'", "\"", "\\", " ", "\t", "\n", "&", "|", ";", "<", ">", "$", "{", "}", "=", ":", "(", ")",
									"0"-"9", "@", "*", "#", "?", "_", "$", "!"] >
|	< #def_DO_SPECIAL			:  ["0"-"9", "@", "*", "#", "?", "_", "$", "!"] >
}

TOKEN:
{ // operators
	< AND_IF	: "&&" >|	< OR_IF		: "||" >
|   < DSEMI		: ";;" >
|	< DLESS		: "<<" >
|	< DGREAT	: ">>" >
|	< LESSAND	: "<&" >
|	< GREATAND	: ">&" >
|	< LESSGREAT	: "<>" >
|	< DLESSDASH : "<<-" > 
|	< CLOBBER	: ">|" >

// first letters of operators
|	< UPPERSANT	: "&" >
|	< PIPE 		: "|" >
|	< SEMICOLON : ";" >  
|	< LESS  	: "<" >  
|	< GREAT  	: ">" >  

// reserved words
|	< IF  		: "if" >  
|	< THEN 		: "then" >  
|	< ELSE 		: "else" >  
|	< ELIF 		: "elif" >  
|	< FI  		: "fi" >  
//|	< DO  		: "do" >  
//|	< DONE 		: "done" >  
|	< CASE 		: "case" >  
|	< ESAC 		: "esac" >  
//|	< WHILE		: "while" >  
//|	< UNTIL		: "until" >  
|	< FOR		: "for" >  

// other reserved words
// These are reserved words, not operator tokens, and are
// recognized when reserved words are recognized.
|	< LBRACE	: "{" >  
|	< RBRACE	: "}" >  
|	< BANG		: "!" >  
|	< IN		: "in" >  

// other special chars
|	< COLON		: ":" >
|	< KLAMMER_AUF:	"(" >
|	< KLAMMER_ZU:	")" >
}

// reached on "${"
<DOLLAR_LBRACE> TOKEN:
{
	< _RBRACE 	: <RBRACE> >								{ parser.popState(); }
|	< COLON_OP	: ":-"|"-"|":="|"="|":?"|"?"|":+"|"+"|"#"|"##"|"%"|"%%" >	{ parser.pushState(DEFAULT); }
}	

< DEFAULT, BACKTICKED, DOLLAR_LBRACE > TOKEN:
{
	< ANY_ESCAPED_CHAR			: "\\" < BANYTHING > >
}

< DOLLAR_LBRACE_VARNAME > TOKEN:
{
	< DS_VARNAME	: (~[" ", "\t", "%", "#", ":", "}"])+ >		{ parser.popState(); }
}

< DEFAULT, DQUOTED > TOKEN:
{	< BACKTIC_START			: < BACKTIC > > { parser.pushState(BACKTICKED); }
|	< DOLLAR_LBRACE_START	: "${"	>		{ parser.pushState(DOLLAR_LBRACE); 
											  parser.pushState(DOLLAR_LBRACE_VARNAME); }
|	< DOLLAR				: "$"	>		{ parser.pushState(DOLLAR_ONLY); }
}

// reached on "$"
<DOLLAR_ONLY> TOKEN:
{
	< DO_SPECIAL	:< def_DO_SPECIAL > >									{ parser.popState(); }
|	< DO_MULTI		:< def_DA_AND_DO_SPECIAL > (<def_DEFAULT_ANYTHING >)? >	{ parser.popState(); }
}

/** no escaped chars possible in SQUOTED */
<SQUOTED> TOKEN:
{
	< SQUOTE_END: < SQUOTE > > { parser.popState(); }	 // :DEFAULT	
|	< SANYTHING	: (~["'"])+ >
}


/** In BACKTICKED we have to find the end of the backtic state. Anything else is one unit (which will be
	later parsed when substituted).
*/
<BACKTICKED > TOKEN:
{
	< BACKTIC_ANYTHING	: (~["`", "\\"])+ >
|	< BACKTIC_END 		: < BACKTIC > >	{ parser.popState(); }
}

// TODO: find a way to parse "=" in Assignment
<DEFAULT> TOKEN:
{
	< SQUOTE_START	: < SQUOTE > > { parser.pushState(SQUOTED); }
|	< DQUOTE_START	: < DQUOTE > > { parser.pushState(DQUOTED); }
|	< WS			: (" "|"\t")+  >
|	< NEWLINE		: "\n" | "\r\n" >
}

<DEFAULT> TOKEN:
{
	< DEFAULT_ANYTHING		: < def_DEFAULT_ANYTHING > >
}

/** According to 2.2.3
The backslash shall retain its special meaning as an escape character only when
followed by one of the following characters when considered special:
$   `   "   \   <newline>
*/
<DQUOTED> TOKEN:
{
	< ESC_DQUOTE			: "\\\"" >
|	< ESC_DOLLAR			: "\\$" >
|	< ESC_BACKTIC			: "\\`" >
|	< ESC_BACKSLASH			: < DEF_ESC_BACKSLASH > >
	/* a backslash is a simple char if not followed by one of the above. */
|	< CHAR_BACKSLASH		: < BQUOTE > >
|	< DQUOTE_END			: < DQUOTE > >	{ parser.popState(); }/* : DEFAULT */
	/** anything else */
|	< DANYTHING				: (~["\"", "$", "`", "\\"])+ >
}

L1Node default_part():
{
StringBuilder sb=new StringBuilder();
Token t;
}
{
	( 
	(	t=< ANY_ESCAPED_CHAR >
|	 	t=< DEFAULT_ANYTHING > 
|	 	t=< BANG > 
	) { sb.append(t.image); }
	)+
	{ return new SimpleL1Node(sb.toString()); }
}

DollarSubstNode dollar_subst():
{
	DollarSubstNode ret=new DollarSubstNode();
	StringBuilder sb=new StringBuilder();
	L1Node wordPartList;
	L1Node varnamePart;
	Token t;
}{
	(	< DOLLAR_LBRACE_START >
	t=<DS_VARNAME>					{ ret.setParameter(new SimpleL1Node(t.image)); }
	(
		t=< COLON_OP >				{ ret.setOperator(new SimpleL1Node(t.image)); }		wordPartList=word_part_list()	{ 
			ret.setWord(wordPartList);

			popState();
			// switch the currents token kind to _RBRACE
			Token token=getToken(1);
			if(token.kind==RBRACE) {
				if(DEBUG)
					System.out.println("switching token kind from RBRACE to _RBRACE");
				token.kind=_RBRACE;
			} else 
				throw new RuntimeException("did not work, kind="+jj_ntk);
		}
	)?
	< _RBRACE >
	) { return ret; }
}

DollarVarNode dollar_var():
{
	Token t;}{
	< DOLLAR >
	( t=< DO_SPECIAL >
|	  t=< DO_MULTI > )
	{ return new DollarVarNode(t); }}

/** Creates a List of L1Node, types are SimpleL1Node or Backticked. */
// TODO change to DQuoteNode, because one must know for all subnodes that these are part of a DQuoted,
// and an empty List makes problems.
DQuotedL1Node dquoted():
{
	DQuotedL1Node ret=new DQuotedL1Node();
	Token t;
	BacktickedNode bn;
}
{
	< DQUOTE_START >
	(
		  t=< ESC_DQUOTE >		{ ret.add(new SimpleL1Node("\"")); }		| t=< ESC_DOLLAR >		{ ret.add(new SimpleL1Node("$")); }
		| t=< ESC_BACKTIC >		{ ret.add(new SimpleL1Node("`")); }
		| t=< ESC_BACKSLASH >	{ ret.add(new SimpleL1Node("\\")); }
		| t=< CHAR_BACKSLASH >	{ ret.add(new SimpleL1Node("\\")); }
		| t=< DANYTHING >		{ ret.add(new SimpleL1Node(t.image)); }
		| bn=backticked()		{ ret.add(bn); }	)*
	< DQUOTE_END >
	{ return ret; }
}

/** Creates a BacktickedNode representing what is found between the two backtics.
This is stored as a String in the node for later parsing.
*/
BacktickedNode backticked():
{
	final StringBuilder sb=new StringBuilder();
	Token t;
}
{
	< BACKTIC_START >	(
		t=< ANY_ESCAPED_CHAR > { sb.append(t.image); }	
	|	t=< BACKTIC_ANYTHING > { sb.append(t.image); }
	)*
	< BACKTIC_END > {
			// TODO could check if number of ", ' and ` are even and/or nested wrong
			// But the subsequent parsing of the backticked command will do this either
			return new BacktickedNode(sb.toString());
		}}

/** creates a String representing what is found between the two single-quotes */
SQuotedL1Node squoted():
{	final StringBuilder sb=new StringBuilder();
	Token t;
}{	<SQUOTE_START>
	( t=<SANYTHING> { sb.append(t.image); } )?
	<SQUOTE_END> {
		return new SQuotedL1Node(sb.toString());
	}}

L2Token separating_part():
{
	Token t;}{
	(	t=< AND_IF >
|	t=< OR_IF >
|   t=< DSEMI >
|	t=< DLESS >
|	t=< DGREAT >
|	t=< LESSAND >
|	t=< GREATAND >
|	t=< LESSGREAT >
|	t=< DLESSDASH > 
|	t=< CLOBBER >
// Not sure what to do with these.
// They must be parsed, they separate words.
// But they are no Operators. ???
// However, since OperatorL1Node simple acts as a word separator its ok to add them here.
|	t=< UPPERSANT >
|	t=< PIPE >
|	t=< SEMICOLON >
|	t=< LESS >
|	t=< GREAT >
// these are no operators, but separate words, too
|	t=< WS > // Note: WS is not sent to HshParser, but separates words.
|	t=< NEWLINE >
|	t=< LBRACE >
|	t=<	RBRACE >
	) {
		return new L2Token(translateL1KindToL2Kind(t.kind), t.image);
	}
}

/** part of a word or whole word.
* Result of parsing on L1-Level is a stream of word_part, which are
* then concatenated to words according to several rules.
* These words are the tokens for parsing on L2-Level.
* A word_part is a  L1Nodes which is the content of that part of a word.
* Which word_parts are concatenated to form complete words, and which ones act as
* word separators is implemented in L2TokenManager.
*/
L1Node word_part():
{
	L1Node ret;
}
{
	(
	ret=default_part()
|	ret=backticked()	
|	ret=dquoted()		
|	ret=squoted()
|	ret=dollar_subst()
|	ret=dollar_var()
	)
	{ return ret; }
}

L2Token word_part_list():
{	L2Token ret=new L2Token();
	L1Node part;
}{		(	part=word_part()	{ ret.addPart(part); }	)+
		{ return ret; }}

/** A word is a separating_part() or a concatenation of word_part() or <EOF>
*/
L2Token word():
{
	L2Token ret;
	Token t;}{
	( 
			ret=word_part_list()
		|	ret=separating_part()
		|	t=<EOF> { ret=new L2Token(L1ParserConstants.EOF, "<EOF>"); }	) { return ret; }}
