options
{
	STATIC=false;
	USER_TOKEN_MANAGER=false;
	FORCE_LA_CHECK=true;
	TOKEN_MANAGER_USES_PARSER=true;
	DEBUG_PARSER=false;
	DEBUG_TOKEN_MANAGER=false;
}
PARSER_BEGIN(L1Parser)
/** http://pubs.opengroup.org/onlinepubs/009695399/utilities/xcu_chap02.html#tag_02_02
 ** 2.3 Token Recognition
* The shell shall read its input in terms of lines from a file, from a terminal in the case of an interactive shell,
* or from a string in the case of sh -c or system(). The input lines can be of unlimited length.
* These lines shall be parsed using two major modes: ordinary token recognition and processing of here-documents.

* When an io_here token has been recognized by the grammar (see Shell Grammar), one or more of the subsequent lines
* immediately following the next NEWLINE token form the body of one or more here-documents and shall be parsed according
* to the rules of Here-Document.

* When it is not processing an io_here, the shell shall break its input into tokens by applying the first applicable
* rule below to the next character in its input. The token shall be from the current position in the input until a
* token is delimited according to one of the rules below; the characters forming the token are exactly those in the
* input, including any quoting characters. If it is indicated that a token is delimited, and no characters have been
* included in a token, processing shall continue until an actual token is delimited.

* 1. If the end of input is recognized, the current token shall be delimited. If there is no current token,
* 	the end-of-input indicator shall be returned as the token.

* 2. If the previous character was used as part of an operator and the current character is not quoted and can be used
* 	with the current characters to form an operator, it shall be used as part of that (operator) token.

* 3. If the previous character was used as part of an operator and the current character cannot be used with the current
* 	characters to form an operator, the operator containing the previous character shall be delimited.

* 4. If the current character is backslash, single-quote, or double-quote ( '\', '", or ' )' and it is not quoted,
* 	it shall affect quoting for subsequent characters up to the end of the quoted text. The rules for quoting are as
* 	described in Quoting. During token recognition no substitutions shall be actually performed, and the result token
* 	shall contain exactly the characters that appear in the input (except for <newline> joining), unmodified, including
* 	any embedded or enclosing quotes or substitution operators, between the quote mark and the end of the quoted text.
* 	The token shall not be delimited by the end of the quoted field.

* 5. If the current character is an unquoted '$' or '`', the shell shall identify the start of any candidates for
* 	parameter expansion ( Parameter Expansion), command substitution ( Command Substitution), or arithmetic expansion
* 	( Arithmetic Expansion) from their introductory unquoted character sequences: '$' or "${", "$(" or '`', and
*	 "$((", respectively. The shell shall read sufficient input to determine the end of the unit to be expanded
*	(as explained in the cited sections). While processing the characters, if instances of expansions or quoting are
*	found nested within the substitution, the shell shall recursively process them in the manner specified for the
*	construct that is found. The characters found from the beginning of the substitution to its end, allowing for any
*	recursion necessary to recognize embedded constructs, shall be included unmodified in the result token, including any
*	embedded or enclosing substitution operators or quotes. The token shall not be delimited by the end of the substitution.

* 6. If the current character is not quoted and can be used as the first character of a new operator, the current token
* 	(if any) shall be delimited. The current character shall be used as the beginning of the next (operator) token.

* 7. If the current character is an unquoted <newline>, the current token shall be delimited.

* 8. If the current character is an unquoted <blank>, any token containing the previous character is delimited and the
* 	current character shall be discarded.

* 9. If the previous character was part of a word, the current character shall be appended to that word.

* 10. If the current character is a '#', it and all subsequent characters up to, but excluding, the next <newline>
* shall be discarded as a comment. The <newline> that ends the line is not considered part of the comment.

* 11. The current character is used as the start of a new word.

* Once a token is delimited, it is categorized as required by the grammar in Shell Grammar.

********************************************************************************************
2.2.3 Double-Quotes

Enclosing characters in double-quotes ( "" ) shall preserve the literal value of all characters within the double-quotes,
with the exception of the characters dollar sign, backquote, and backslash, as follows:

$
The dollar sign shall retain its special meaning introducing parameter expansion (see Parameter Expansion), a form of
command substitution (see Command Substitution), and arithmetic expansion (see Arithmetic Expansion).
The input characters within the quoted string that are also enclosed between "$(" and the matching ')' shall not be
affected by the double-quotes, but rather shall define that command whose output replaces the "$(...)" when the word is
expanded. The tokenizing rules in Token Recognition , not including the alias substitutions in Alias Substitution , shall
be applied recursively to find the matching ')'.

Within the string of characters from an enclosed "${" to the matching '}', an even number of unescaped double-quotes or
single-quotes, if any, shall occur. A preceding backslash character shall be used to escape a literal '{' or '}'.
The rule in Parameter Expansion shall be used to determine the matching '}' .

`
The backquote shall retain its special meaning introducing the other form of command substitution
(see Command Substitution). The portion of the quoted string from the initial backquote and the characters up to the
next backquote that is not preceded by a backslash, having escape characters removed, defines that command whose output
replaces "`...`" when the word is expanded.
Either of the following cases produces undefined results:
A single-quoted or double-quoted string that begins, but does not end, within the "`...`" sequence
A "`...`" sequence that begins, but does not end, within the same double-quoted string

\
The backslash shall retain its special meaning as an escape character (see Escape Character (Backslash)) only when
followed by one of the following characters when considered special:
$   `   "   \   <newline>

The application shall ensure that a double-quote is preceded by a backslash to be included within double-quotes.
The parameter '@' has special meaning inside double-quotes and is described in Special Parameters.
********************************************************************************************
http://pubs.opengroup.org/onlinepubs/009695399/utilities/xcu_chap02.html#tag_02_09_01
The following are the operators mentioned above.


AND_IF    OR_IF    DSEMI
 '&&'      '||'     ';;'    


DLESS  DGREAT  LESSAND  GREATAND  LESSGREAT  DLESSDASH
 '<<'   '>>'    '<&'     '>&'      '<>'       '<<-'   


 CLOBBER
   '>|'

* ******************************************************************************************
* Tokenizer States tree:
* -DEFAULT
*	-BACKSLASH escapes any following char: BQUOTED
*		-following char : DEFAULT
*	-SINGLEQUOTE escapes all chars: SQUOTED
*		-SINGLEQUOTE : DEFAULT
*	-DOUBLEQOUTE : DQUOTED
*		-BACKSLASH escapes: $, BACKTIC, BACKSLASH, DOUBLEQUOTE
*		-$ starts substitution : DOUBLEQUOTE_DOLLAR
*		-BACKTIC start substitution : DOUBLEQUOTE_BACKTICKED
*		-DOUBLEQUOTE : DEFAULT
*	-BACKTIC : DEFAULT_BACKTICKED
*		--TODO
*/

package com.happypeople.hsh.hsh.l1parser;
import java.util.*;
import com.happypeople.hsh.hsh.L2Token;
import com.happypeople.hsh.hsh.parser.L2Node;
import com.happypeople.hsh.hsh.HshParserConstants;
import com.happypeople.hsh.hsh.HshParser;
import com.happypeople.hsh.hsh.l1parser.L2TokenManager;

public class L1Parser // implements TokenManager
{
	final boolean DEBUG=false;
	
	/** Interface for token-translation between L1Parser and L2Parser
	*/
	public L2Token nextL1Node() throws ParseException {
		L2Token t;
		do {			word(t=new L2Token());
			if(DEBUG)
				System.out.println("nextL1Node: "+t.kind+" "+t.image);
		} while(t.kind==WS); // skip WS
		return t;
	}

	/** This needs to be defined, because in L2Parser there is no WS.
	*	Note that WS-Token are not sent to L2Parser.
	* 	It is defined as -42, because this is an unused number for Token-constants (they are all >=0)
	*/
	private final static int WS_KIND=-42;
	private final static int UNDEFINED_KIND=-1;

	/** Interface for dynamic extra actions. */
	interface Action {
		public void after();
	}
	
	private GenericL1ParserTokenManager.Callback genCallback=new GenericL1ParserTokenManager.Callback() {
		public Token nextToken() {
			return getNextToken();
		}
	};
}

PARSER_END(L1Parser)

TOKEN_MGR_DECLS:{	final boolean DEBUG=false;

	/** Stack of states. Usefull to switch to "previous" state. Initialized to DEFAULT. */
	private LinkedList<Integer> stateStack=new LinkedList<Integer>(Arrays.asList(DEFAULT));

	/** Called by TokenManager */
	public void pushState(int state) {
		if(DEBUG)
			System.out.println("pushState: "+state);
		stateStack.push(state);
		SwitchTo(state);
	}

	/** Called by TokenManager */
	public void popState() {
		final int oldstate=stateStack.pop();
		final int state=stateStack.peek();
		if(DEBUG)
			System.out.println("popState, from:"+oldstate+" to:"+state);
		SwitchTo(state);
	}
}
	
TOKEN:
{	< #SQUOTE: "'" >
|	< #DQUOTE: "\"" > 
|	< #BQUOTE: "\\" >
|	< #BACKTIC:	"`" >
|	< #BANYTHING: ~[] >
|	< #DEF_ESC_BACKSLASH	: "\\\\" >
|	< #def_DEFAULT_ANYTHING	: (~["`", "'", "\"", "\\", 
								" ", "\t", "\r", "\n", 
								"&", "|", ";", "$", "{", "}", "(", ")", "<", ">", "=" ])+ >
	// chars of DEFAULT_ANYTHING and DO_SPECIAL
|	< #def_DA_AND_DO_SPECIAL	:  ~["`", "'", "\"", "\\", " ", "\t", "\n", "&", "|", ";", "$", "{", "}", "=", ":",
									"0"-"9", "@", "*", "#", "?", "_", "$", "!"] >
|	< #def_DO_SPECIAL			:  ["0"-"9", "@", "*", "#", "?", "_", "$", "!"] >
|	< #def_NUMBER				: (["0"-"9"])+ >
|	< #def_NAME					: ["_", "a"-"z", "A"-"Z"](["_", "a"-"z", "A"-"Z", "0"-"9"])* >
}

TOKEN:
{ // operators
	< AND_IF	: "&&" >|	< OR_IF		: "||" >
|   < DSEMI		: ";;" >
|	< DLESS		: "<<" >
|	< DGREAT	: ">>" >
|	< LESSAND	: "<&" >
|	< GREATAND	: ">&" >
|	< LESSGREAT	: "<>" >
|	< DLESSDASH : "<<-" > 
|	< CLOBBER	: ">|" >

// first letters of operators
|	< UPPERSANT	: "&" >
|	< PIPE 		: "|" >
|	< SEMICOLON : ";" >  
|	< LESS  	: "<" >  
|	< GREAT  	: ">" >  

// reserved words
//|	< IF  		: "if" >  
//|	< THEN 		: "then" >  
//|	< ELSE 		: "else" >  
//|	< ELIF 		: "elif" >  
//|	< FI  		: "fi" >  
//|	< DO  		: "do" >  
//|	< DONE 		: "done" >  
//|	< CASE 		: "case" >  
//|	< ESAC 		: "esac" >  
//|	< WHILE		: "while" >  
//|	< UNTIL		: "until" >  
//|	< FOR		: "for" >  

// other reserved words
// These are reserved words, not operator tokens, and are
// recognized when reserved words are recognized.
|	< LBRACE	: "{" >  
|	< RBRACE	: "}" >  
|	< BANG		: "!" >  
//|	< IN		: "in" >  

// other special chars
|	< COLON			: ":" >
|	< KLAMMER_AUF	: "(" >
|	< KLAMMER_ZU	: ")" >
|	< EQUALS		: "=" >
}

// reached on "${"
<DOLLAR_LBRACE> TOKEN:
{
	< _RBRACE 	: <RBRACE> >								{ popState(); }
|	< COLON_OP	: ":-"|"-"|":="|"="|":?"|"?"|":+"|"+"|"#"|"##"|"%"|"%%" >	{ pushState(DEFAULT); }
}	

< DEFAULT, BACKTICKED, DOLLAR_LBRACE > TOKEN:
{
	< ANY_ESCAPED_CHAR			: "\\" < BANYTHING > >
}

< DOLLAR_LBRACE_VARNAME > TOKEN:
{
	< DS_VARNAME	: (~[" ", "\t", "%", "#", ":", "}"])+ >		{ popState(); }
}

< DEFAULT, DQUOTED > TOKEN:
{	< BACKTIC_START			: < BACKTIC > > { pushState(BACKTICKED); }
|	< DOLLAR_LBRACE_START	: "${"	>		{ pushState(DOLLAR_LBRACE); 
											  pushState(DOLLAR_LBRACE_VARNAME); }
|	< DOLLAR_EXEC_START		: "$("	>
|	< DOLLAR				: "$"	>		{ pushState(DOLLAR_ONLY); }
}

// reached on "$"
<DOLLAR_ONLY> TOKEN:
{
	< DO_SPECIAL	:< def_DO_SPECIAL > >									{ popState(); }
|	< DO_MULTI		:< def_DA_AND_DO_SPECIAL > (<def_DEFAULT_ANYTHING >)? >	{ popState(); }
}

/** no escaped chars possible in SQUOTED */
<SQUOTED> TOKEN:
{
	< SQUOTE_END: < SQUOTE > > { popState(); }	 // :DEFAULT	
|	< SANYTHING	: (~["'"])+ >
}


/** In BACKTICKED we have to find the end of the backtic state. Anything else is one unit (which will be
	later parsed when substituted).
*/
<BACKTICKED > TOKEN:
{
	< BACKTIC_ANYTHING	: (~["`", "\\"])+ >
|	< BACKTIC_END 		: < BACKTIC > >	{ popState(); }
}

<DEFAULT> TOKEN:
{
	< SQUOTE_START	: < SQUOTE > > { pushState(SQUOTED); }
|	< DQUOTE_START	: < DQUOTE > > { pushState(DQUOTED); }
|	< WS			: (" "|"\t")+  >
|	< NEWLINE		: "\n" | "\r\n" >
}

<DEFAULT> TOKEN:
{
	< NAME					: < def_NAME > >
|	< NUMBER				: < def_NUMBER > >
|	< DEFAULT_ANYTHING		: < def_DEFAULT_ANYTHING > >
}

/** According to 2.2.3
The backslash shall retain its special meaning as an escape character only when
followed by one of the following characters when considered special:
$   `   "   \   <newline>
*/
<DQUOTED> TOKEN:
{
	< ESC_DQUOTE			: "\\\"" >
|	< ESC_DOLLAR			: "\\$" >
|	< ESC_BACKTIC			: "\\`" >
|	< ESC_BACKSLASH			: < DEF_ESC_BACKSLASH > >
	/* a backslash is a simple char if not followed by one of the above. */
|	< CHAR_BACKSLASH		: < BQUOTE > >
|	< DQUOTE_END			: < DQUOTE > >	{ popState(); }/* : DEFAULT */
	/** anything else */
|	< DANYTHING				: (~["\"", "$", "`", "\\"])+ >
}

L1Node default_part(ImageHolder l2token):
{
L1Node node=null;
Token t;
int off=l2token.getLen();
}
{
	(	t=< ANY_ESCAPED_CHAR > { node=new BackslashQuotedL1Node(l2token, off, t.image.length()); }|	 	t=< NUMBER > 
|	 	t=< NAME > 
|	 	t=< DEFAULT_ANYTHING > 
|	 	t=< BANG > 
|	 	t=< LESS > 
|	 	t=< GREAT >
|		t=< EQUALS >
	)
	{ 
		l2token.append(t.image);
		if(node==null)
			return new SimpleL1Node(l2token, off, l2token.getLen()-off, t.kind);
		return node;
	}
}

L1Node dollar_exec(ImageHolder l2Token):
{
Token t;
L2Node subshell;
int off=l2Token.getLen();

// insert one artificial token after the current DOLLAR_EXEC_START token
Token klammerAufTok=new Token(KLAMMER_AUF, "(");
klammerAufTok.next=getToken(1).next;
getToken(1).next=klammerAufTok;
}
{
	t=< DOLLAR_EXEC_START >		{ l2Token.append(t.image); 
		// * instantiate HshParser
		// * instantiate L1Parser as TokenManager for instantiated HshParser
		// * instantiate a generic TokenManager for instantiated L1Parser
		// * feed L1-tokens generated by the tokenManager of this parser into the generic TokenManager, 
		//   until KLAMMER_ZU. Or call hshParser.subshell(), which will suck tokens until KLAMMER_ZU.
		// * put the L2Token generated by hshParser.subshell() into l2Token, and return.
		L1ParserTokenManager genTokManager=new GenericL1ParserTokenManager(genCallback);
		L1Parser genParser=new L1Parser(genTokManager);
		L2TokenManager l2TokManager=new L2TokenManager(genParser);
		HshParser hshParser=new HshParser(l2TokManager);
		try {
			subshell=hshParser.subshell();
		}catch(com.happypeople.hsh.hsh.ParseException e) {
			e.printStackTrace(System.out);
			throw new ParseException();
		}

	}
	/*
	t=< KLAMMER_ZU > this token is consumed by the hshParser above
	*/			{ 
		l2Token.append(subshell.getImage());
		DollarExecNode ret=new DollarExecNode(l2Token, off, l2Token.getLen()-off);
		ret.add(subshell.getChild(0));
		return ret;
	}
}

L1Node dollar_subst(ImageHolder l2Token):
{
	L1Node parameter=null;
	L1Node operator=null;
	Token t;
	int off=l2Token.getLen();
	ComplexL1Node wplParent=null;
}{
	(	t=< DOLLAR_LBRACE_START >		{ l2Token.append(t.image); }
	t=<DS_VARNAME>					{ parameter=new SimpleL1Node(l2Token, l2Token.getLen(), t.image.length());
										l2Token.append(t.image); }
	(
		t=< COLON_OP >				{ operator=new SimpleL1Node(l2Token, l2Token.getLen(), t.image.length());
										l2Token.append(t.image); }		word_part_list(l2Token, wplParent=new GenericComplexL1Node(l2Token, l2Token.getLen(), 0))	{
			// TODO: this needs to be reworked. Swithing tokenizer-state in the parser is
			// very, very bad, since there could be arbitrary lookaheads...
			token_source.popState();
			// switch the currents token kind to _RBRACE
			Token token=getToken(1);
			if(token.kind==RBRACE) {
				if(DEBUG)
					System.out.println("switching token kind from RBRACE to _RBRACE");
				token.kind=_RBRACE;
			} else 
				throw new RuntimeException("did not work, kind="+jj_ntk);
				
			wplParent.addLen(l2Token.getLen()-wplParent.getOff());
		}
	)?
	t=< _RBRACE >	{ l2Token.append(t.image); }
	) { 
		DollarSubstNode ret=new DollarSubstNode(l2Token, off, l2Token.getLen()-off);
		if(parameter!=null)
			ret.setParameter(parameter);
		if(operator!=null)
			ret.setOperator(operator);
		if(wplParent!=null) {
			ret.setWord(wplParent);
		}
		return ret;
	}
}

L1Node dollar_var(ImageHolder l2token):
{
	Token t;
}{
	< DOLLAR >
	( t=< DO_SPECIAL >
|	  t=< DO_MULTI > )
	{ L1Node ret=new DollarVarNode(l2token, l2token.getLen(), t.image.length()+1, t);
		l2token.append("$").append(t.image);
		return ret;
	}}

/** A double quoted part. There can be backticked and dollar- subparts. */
L1Node dquoted(ImageHolder imageHolder):
{
	Token t=null;
	String simpleImage=null;
	L1Node childNode=null;
	final int off=imageHolder.getLen();
	DQuotedL1Node ret=new DQuotedL1Node(imageHolder, off, 0);
}
{
	< DQUOTE_START > { imageHolder.append("\""); }
	(
		(		  < ESC_DQUOTE >		{ simpleImage="\""; }		| < ESC_DOLLAR >		{ simpleImage="$"; }
		| < ESC_BACKTIC >		{ simpleImage="`"; }
		| < ESC_BACKSLASH >		{ simpleImage="\\"; }
		| < CHAR_BACKSLASH >	{ simpleImage="\\"; }
		| t=< DANYTHING >		{ simpleImage=t.image; }
		| childNode=backticked(imageHolder)
		| childNode=dollar_var(imageHolder)
		| childNode=dollar_subst(imageHolder)
		| childNode=dollar_exec(imageHolder)
		)
		{
			if(simpleImage!=null) {				childNode=new SimpleL1Node(imageHolder, imageHolder.getLen(), simpleImage.length());
				imageHolder.append(simpleImage);
				simpleImage=null;
			}
			ret.add(childNode);		}	)*
	< DQUOTE_END >
	{ 	imageHolder.append("\"");
		ret.addLen(imageHolder.getLen()-off);
		return ret;
	}
}

/** Creates a BacktickedNode representing what is found between the two backtics.
This is stored as a String in the node for later parsing.
*/
L1Node backticked(ImageHolder l2token):
{
Token t;
int off=l2token.getLen();
}
{
	< BACKTIC_START >	{ l2token.append("`"); }		(
		t=< ANY_ESCAPED_CHAR > { l2token.append(t.image); }	
	|	t=< BACKTIC_ANYTHING > { l2token.append(t.image); }
	)*
	< BACKTIC_END >		{ l2token.append("`");
			// TODO could check if number of ", ' and ` are even and/or nested wrong
			// But the subsequent parsing of the backticked command will do this either
			BacktickedNode ret=new BacktickedNode(l2token, off, l2token.getLen()-off);
			return ret;
		}}

/** Single quoted part of a word */
L1Node squoted(ImageHolder imageHolder):
{Token t;
int off=imageHolder.getLen();
}{	<SQUOTE_START>	{ imageHolder.append("'"); }	( t=<SANYTHING>	{ imageHolder.append(t.image); } )?
	<SQUOTE_END>	{ imageHolder.append("'");
		return new SQuotedL1Node(imageHolder, off, imageHolder.getLen()-off);
	}}

void separating_part(ImageHolder imageHolder, L2Token parent):
{
	Token t;}{
	(	t=< AND_IF >
|	t=< OR_IF >
|   t=< DSEMI >
|	t=< DLESS >
|	t=< DGREAT >
|	t=< LESSAND >
|	t=< GREATAND >
|	t=< LESSGREAT >
|	t=< DLESSDASH > 
|	t=< CLOBBER >
// Not sure what to do with these.
// They must be parsed, they separate words.
// But they are no Operators. ???
|	t=< UPPERSANT >
|	t=< PIPE >
|	t=< SEMICOLON >
|	t=< KLAMMER_AUF >
|	t=< KLAMMER_ZU >
// these are no operators, but separate words, too
|	t=< WS > // Note: WS is not sent to HshParser, but separates words.
|	t=< NEWLINE >
	) {
		parent.kind=t.kind;
		parent.addPart(new SimpleL1Node(imageHolder, imageHolder.getLen(), t.image.length(), t.kind));
		imageHolder.append(t.image);
	}
}

/** part of a word 
* Result of parsing on L1-Level is a stream of word_part, which are
* then concatenated to words according to several rules.
* These words are the tokens for parsing on L2-Level.
* A word_part is a  L1Nodes which is the content of that part of a word.
* Which word_parts are concatenated to form complete words, and which ones act as
* word separators is implemented in L2TokenManager.
*/
L1Node word_part(ImageHolder imageHolder):
{
	L1Node child;
}
{
	(
	child=backticked(imageHolder)	
|	child=dquoted(imageHolder)		
|	child=squoted(imageHolder)
|	child=dollar_subst(imageHolder)
|	child=dollar_exec(imageHolder)
|	child=dollar_var(imageHolder)
|	child=default_part(imageHolder)
	)
	{ return child; }
}

/** A word_part_list is a L2Token
*/
void word_part_list(ImageHolder imageHolder, ComplexL1Node parent):
{
L1Node node;
}
{		( node=word_part(imageHolder) { parent.add(node); } )+
}

/** A word is a separating_part() or a concatenation of word_part() or <EOF>
*/
void word(L2Token ret):
{
	Token t;
	L1Node list=null;
	L1Node part;
	ComplexL1Node wplParent=null;
	SimpleImageHolder imageHolder=new SimpleImageHolder();
}{
	( 
			word_part_list(imageHolder, wplParent=new GenericComplexL1Node(imageHolder, 0, 0)) {
				for(L1Node node : wplParent)
					ret.addPart(node);
				ret.kind=HshParserConstants.WORD;
			}
		|	separating_part(imageHolder, ret)
		|	t=<EOF> {
						ret.kind=HshParserConstants.EOF;
						// empty image
					}	)
	{
		//ret.finishImage();
	}}
